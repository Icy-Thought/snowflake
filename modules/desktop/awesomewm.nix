{
  options,
  config,
  lib,
  pkgs,
  ...
}:
with lib;
with lib.my; let
  cfg = config.modules.desktop.awesomewm;
  configDir = config.snowflake.configDir;
in {
  options.modules.desktop.awesomewm = {
    enable = mkBoolOpt false;
    lua.enable = mkBoolOpt false;
    fennel.enable = mkBoolOpt false;
  };

  config = mkIf cfg.enable {
    nixpkgs.overlays = [
      (self: super: {
        awesome = super.awesome.override {lua = super.luajit;};
      })
    ];

    environment.systemPackages = with pkgs;
      mkMerge [
        # AwesomeWM Dependencies
        luajit
        (mkIf cfg.fennel.enable [my.fennel])

        # Extra Dependencies
        lightdm
        libnotify
        playerctl
        gxmessage
        xdotool
        xclip
        feh
      ];

    modules.desktop = {
      media.browser.nautilus.enable = true;
      extra = {
        customLayout.enable = true;
        fcitx5.enable = true;
        mimeApps.enable = true; # mimeApps -> default launch application
        picom.enable = true;
        rofi.enable = true;
      };
    };

    services.xserver = {
      enable = true;
      displayManager = {
        defaultSession = "none+awesome";
        lightdm.enable = true;
        lightdm.greeters.mini.enable = true;
      };
      windowManager.awesome.enable = true;
    };

    home.configFile = mkMerge [
      (mkIf cfg.lua.enable {
        "awesome" = {
          source = "${configDir}/awesomewm/lua";
          recursive = true;
        };

        "awesome/rc.lua".text = ''
          -- The following file was auto-generated by Nix.
          -- REFRAIN FROM CONTAMINATING THIS FILE!

          pcall(require, "luarocks.loader")

          local gears = require("gears")
          local beautiful = require("beautiful")

          local theme_dir = gears.filesystem.get_configuration_dir() .. "theme/"
          beautiful.init(theme_dir .. "theme.lua")

          require("configuration")
          require("helpers")
          require("modules")
          require("ui")

          -- Garbage collection
          collectgarbage("setpause", 110)
          collectgarbage("setstepmul", 1000)
          gears.timer({
              timeout = 5,
              autostart = true,
              call_now = true,
              callback = function()
                  collectgarbage("collect")
              end,
          })
        '';
      })

      (mkIf cfg.fennel.enable {
        "awesome" = {
          source = "${configDir}/awesomewm/fennel";
          recursive = true;
        };

        "awesome/rc.lua".text = ''
          -- The following file was auto-generated by Nix.
          -- REFRAIN FROM CONTAMINATING THIS FILE!

          -- Load system fennel
          package.path = package.path .. ";${pkgs.my.fennel}/?.lua"
          local fennel = require("fennel")
          debug.traceback = fennel.traceback

          -- Then load our awesomewm config
          fennel.path = fennel.path .. ";.config/awesome/?.fnl"
          table.insert(package.loaders or package.searchers, fennel.makeSearcher({
              correlate    = true,
              useMetadata  = true
          }))
          require("init")
        '';
      })
    ];
  };
}
